## Meet OCaml

[OCaml](https://ocaml.org/) is a general purpose programming language with an emphasis on expressiveness and safety.

### First tour

Open the [try site](https://reasonml.github.io/en/try.html?reason=Q) and try each code bellow in the OCaml box, step by step

#### Binding

Variable declaration and assignement use `let` binding :

```OCaml
let greeting: string = "hello"
let score: int = 10
```

Bidings are immutables but you can wrap it with a ref, which is like a box whose content can change.

```OCaml
let score: int ref = ref 10
let _ = score := 5
let five: int = !score
let _ = print_endline (string_of_int five)
```

However, you may create a new binding of the same name which shadows the previous binding :

```OCaml
let greeting: string = "hello"
let _ = print_endline greeting
let greeting: string =  "bye"
let _ = print_endline greeting
```

#### Types

The type system is completely "sound". This means that, as long as your code compiles fine, every type guarantees that it's not lying about itself. In a conventional, best-effort type system, just because the type says it's e.g. "an integer that's never null", doesn't mean it's actually never null. In contrast, a pure Reason program has no null bugs.

OCaml types can be inferred. The type system deduces the types for you even if you don't manually write them down.

```OCaml
let greeting = "hello"
let score = 10
```

Alias types can refer to a type by a different name. They'll be equivalent:

```OCaml
type scoreType = int
let score : scoreType = 10
```

OCaml provides two list primitives : List and Array

```OCaml
(* Lists are homogeneous, immutable, fast at prepending items *)
let heroes : string list = ["Carl"; "Rick"; "Michonne"]
let moreHeroes = "Negan"::heroes (* create a new list preprend by "Negan" *)
(* Arrays are mutable, fast at random access & updates, fix-sized on native (flexibly sized on JavaScript) *)
let heroesArray : string array = [|"Carl"; "Rick"; "Michonne"|]
let carl : string = Array.get heroesArray 0
let _ = Array.set heroesArray 0 "Negan" (* mutate heroesArray replacing "Carl" by  "Negan" *)
```

Product types

```OCaml
(* Tuples are immutable, ordered, fix-sized at creation time heterogeneous *)
let nameAndHeart: (string * int) = ("Negan", 10)
type coord3d = (int * int * int)
let warehouseCoord: coord3d = (1, 4, 18)

(* Records are immutable by default and fixed in field names and types *)
type hero = {
  heart: int;
  name: string;}
let negan = { heart = 10; name = "Negan"}
let name =  "Carl"
let carl = { name; heart = 8 }
```

Variant types

```OCaml
type lifeVariant =
  | Alive of hero
  | Dead
  | Zombi

let howIsNegan = Alive negan
let howIsLori = Dead
```

Alive, Dead and Zombi are called "constructors" (or "tag"). The | bar separates each constructor. A variant's constructors need to be capitalized. Type constructor may have parameters

Algebraic data types come with one of the most important features : **pattern matching**

```Ocaml
let getMessage howAreYou =
  match howAreYou with
  | Zombi -> "Aaaaaarg !"
  | Alive h -> "Great ! " ^ h.name ^ " is alive"
  | Dead -> "!!!"

let message = getMessage howIsNegan
(* message is "Great! Negan is alive" *)
```

#### Options

OCaml itself doesn't have the notion of null or undefined. This is a great thing, as it wipes out an entire category of bugs. No more undefined is not a function, and cannot access foo of undefined!

We represent the existence and nonexistence of a value by wrapping it with the option type. Here's its definition from the standard library:

```Ocaml
type 'a option =
  | None
  | Some of 'a
```

It means "a value of type option is either None (nothing) or that actual value wrapped in a Some".

Exemple :

```Ocaml
type weapon = string option
let isDangerous w =
  match w with
  | None  -> "not dangerous"
  | Some a -> "is armed with a " ^ a
let unArmed: weapon = None
let knife: weapon = Some "knife"
(* OCaml isn't pure and that's fine *)
let _ = print_endline (isDangerous unArmed)
let _ = print_endline (isDangerous knife)
```

#### Functions

Functions are declared with an expression

```OCaml
(* 3 syntaxes produces the same bytecode ! *)
let add: int -> int -> int = fun x y  -> x + y (* Functions are lambas binded with let *)
let add = fun x  -> fun y  -> x + y (* OCaml auto-curry functions as suggested by function type *)
let add x y = x + y (* shorter syntax *)

let _ = let toPrint = add 1 3 in print_endline(string_of_int toPrint)
```

OCaml function are auto-curried. The above multiply is nothing but syntactic sugar for this

```OCaml
let multiply x y = x * y
```

So OCaml functions can automatically be partially called.

```OCaml
let multiply2 = multiply 2
let ten = multiply2 5
```

Another great feature is the pipe opertator which can compose function by piping them

```OCaml
let twenty = (multiply2 5) |> multiply2 (* same as multiply2(multiply2(5)) *)
let _ = (twenty |> string_of_int) |> print_endline (* print_endline(string_of_int(twenty)) *)
```

There is much more amazing features to know in OCaml, we just focus here in those that you should use to solve the tennis kata (￣^￣)ゞ
